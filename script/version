#!/bin/zsh -Ndefgku
#
# script/version
# mas
#
# Displays the mas version.
#

. "${0:a:h}/_setup_script"

# Check if on a tag
tag_at_head=$(git describe --exact-match --tags HEAD 2>/dev/null)
if [[ -n "$tag_at_head" ]]; then
  # Show version without branch prefix
  printf $'%s\n' "${tag_at_head#v}"
  exit 0
fi

branch="$(git rev-parse --abbrev-ref HEAD)"
if [[ "${branch}" == "main" ]]; then
  branch=""
fi

if [[ "${branch}" = HEAD ]]; then
  if git merge-base --is-ancestor HEAD main; then
    branch=
  else
    branch="${"${(@)"${(fnO)"$(git branch --contains HEAD --format '%(ahead-behind:HEAD) %(refname:short)')"}"[1]}"##* }"
  fi
fi

printf $'%s%s%s\n'\
 "${branch:+"${branch}-"}"\
 "${"$(git describe --tags 2>/dev/null)"#v}"\
 "${"$(git diff-index HEAD --;git ls-files --exclude-standard --others)":+"${MAS_DIRTY_INDICATOR-+}"}"
