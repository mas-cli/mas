#!/bin/zsh -Ndfgku
#
# script/lint
# mas
#
# Linting checks for development and CI.
#
# Reports style violations without making any modifications to the code.
#
# Please keep in sync with script/format.
#

. "${0:a:h}/_setup_script"

script/generate_version_info_for_swift

printf $'==> ðŸš¨ Linting mas %s\n' "$(script/version)"

for linter in git markdownlint periphery shellcheck swift-format swiftformat swiftlint yamllint; do
  if [[ ! -x "$(command -v "${linter}")" ]]; then
    printf $'error: %s is not installed. Run \'script/bootstrap\' or \'brew install %s\'.\n' "${linter}" "${linter}" >&2
    exit 1
  fi
done

exit_code=0
for source in Package.swift Sources Tests; do
  printf -- $'--> ðŸ¦… %s swift-format\n' "${source}"
  swift-format lint --strict --recursive "${source}"
  ((exit_code |= ${?}))
  printf -- $'--> ðŸ¦… %s swiftformat\n' "${source}"
  script -q /dev/null swiftformat --lint --strict "${source}" |
    (grep -vxE '(?:\^D\x08{2})?Running SwiftFormat\.{3}\r|\(lint mode - no files will be changed\.\)\r|Reading (?:config|swift-version) file at .*|\x1b\[32mSwiftFormat completed in \d+\.\d+s\.\x1b\[0m\r|0/\d+ files require formatting\.\r' || true)
  ((exit_code |= ${?}))
  printf -- $'--> ðŸ¦… %s swiftlint\n' "${source}"
  swiftlint --strict --quiet "${source}"
  ((exit_code |= ${?}))
done

printf -- $'--> ðŸš ShellCheck\n'
shellcheck -s bash -o all -e SC2296,SC2301,SC2312 -a -P SCRIPTDIR script/**/*(.)
((exit_code |= ${?}))

printf -- $'--> ã€½ï¸ Markdown\n'
markdownlint --config .markdownlint.json . docs
((exit_code |= ${?}))

printf -- $'--> ðŸ“ YAML\n'
yamllint .
((exit_code |= ${?}))

printf -- $'--> ðŸŒ³ Git\n'
git diff --check
((exit_code |= ${?}))

printf -- $'--> ðŸŒ€ Periphery\n'
script -q /dev/null periphery scan --strict --quiet --disable-update-check |
  (grep -vxE '(?:\x1b\[0;1;32m|\^D\x08{2})\* (?:\x1b\[0;0m\x1b\[0;1m)?No unused code detected\.(?:\x1b\[0;0m)?\r' || true)
((exit_code |= ${?}))

exit "${exit_code}"
